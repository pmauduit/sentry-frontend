plugins {
	id 'org.springframework.boot' version '3.0.0'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'groovy'
	id "com.github.node-gradle.node" version "3.5.0"
	id "com.github.ben-manes.versions" version "0.38.0"
}

group = 'fr.spironet'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.groovy:groovy'
    implementation 'org.apache.groovy:groovy-json'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
}

test {
	useJUnitPlatform()
}

/** excludes some npm stuff from artifact */
bootJar {
	exclude("**/static/node_modules")
	exclude("**/static/npm-v*")
	exclude("**/static/package*json")
	exclude("**/static/public")
	exclude("**/static/src")
	exclude("**/static/.idea")
	exclude("**/static/server.js")
}

/** npm stuff */
node {
	version = '12.16.3'
	npmVersion = '6.14.4'
	download = true
	nodeProjectDir = file("${project.rootDir}/src/main/resources/static")
}

task appNpmInstall(type: NpmTask) {
	description = "Installs dependencies from package.json"
	args = ['install']
}

task appNpmRunBuild(type: NpmTask) {
	description = "Compiles my app"
	args = ['run', 'build']
}

assemble.dependsOn appNpmInstall, appNpmRunBuild
